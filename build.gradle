buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

def siteUrl = 'https://github.com/stariy95/dns-client'
def projectDescription = 'Compact DNS client library intended primary for network utilities and testing applications'

group 'com.kendamasoft'
version '1.1.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    testImplementation 'junit:junit:4.13.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

Properties properties = new Properties()
if(project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties')?.newDataInputStream())
}
def ossrhUsername = properties.getProperty('ossrh.user')
def ossrhKey = properties.getProperty('ossrh.key')

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials  {
                username ossrhUsername
                password ossrhKey
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = projectDescription
                url = siteUrl
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'kendamasoft.com'
                    }
                }
                scm {
                    connection = "scm:git:${siteUrl}"
                    url = siteUrl
                }
            }
        }
    }
}

signing {
    required { !version.endsWith("SNAPSHOT") }
    useGpgCmd()
    sign publishing.publications.mavenJava
}

javadoc {
    options.overview = "overview.html"
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

wrapper {
    gradleVersion = '6.8.2'
}