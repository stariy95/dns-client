buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id "com.jfrog.bintray" version "1.4"
}

def currentVersion = '0.9.4'
def siteUrl = 'https://github.com/stariy95/dns-client'      // Homepage URL of the library
def projectDescription = 'DNS Client is a small DNS client library intended primary for network utilities and testing applications'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group 'com.kendamasoft'
    version currentVersion

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        testCompile 'junit:junit:4.7'
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc {
        options.overview = "overview.html"
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar, javadocJar
    }

    repositories {
        jcenter()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId group
                artifactId 'dns-client'
                version currentVersion

                pom.withXml {
                    def root = asNode()
                    root.appendNode('name', 'dns-client')
                    root.appendNode('description', projectDescription)
                    root.appendNode('url', siteUrl)
                    root.appendNode('inceptionYear', '2015')

                    def license = root.appendNode('licenses').appendNode('license')
                    license.appendNode('name', 'The Apache Software License, Version 2.0')
                    license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                    license.appendNode('distribution', 'repo')

                    root.appendNode('developers').appendNode('developer').appendNode('name', 'kendamasoft.com')

                    def scm = root.appendNode('scm')
                    scm.appendNode('connection', "${siteUrl}.git")
                    scm.appendNode('url', siteUrl)
                }

                from components.java

                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def bintrayUser = properties.getProperty('bintray.user')
    def bintrayApiKey = properties.getProperty('bintray.key')

    bintray {
        user = bintrayUser
        key = bintrayApiKey

        publications = ['mavenJava']
        dryRun = false
        publish = true

        pkg {
            repo = 'maven'
            name = 'dns-client'
            userOrg = user

            desc = projectDescription
            websiteUrl = siteUrl
            issueTrackerUrl = "${siteUrl}/issues"
            vcsUrl = "${siteUrl}.git"
            labels = ['DNS', 'client', 'java', 'android']
            licenses = ['Apache-2.0']

            version {
                name = currentVersion
                /*
                mavenCentralSync {
                    sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
                    user = 'userToken' //OSS user token
                    password = 'paasword' //OSS user password
                    close = '1' //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
                }*/
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}